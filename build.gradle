allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = 'org.mbtest.javabank'
    version = "${majorVersion}.${minorVersion}.${patchVersion}-SNAPSHOT"

    if (!project.hasProperty("ossrhUsername")) {
        ext.ossrhUsername = ""
    }

    if (!project.hasProperty("ossrhPassword")) {
        ext.ossrhPassword = ""
    }

    repositories {
        mavenCentral()
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar, javadocJar, sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = project.name
                    description = 'Native Java bindings for Mountebank'
                    url = 'http://github.com/thejamesthomas/javabank'

                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'http://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'thejamesthomas'
                            name = 'James Thomas'
                            url = 'https://github.com/thejamesthomas'
                            email = 'james@duckzilla.com'
                        }
                    }

                    scm {
                        url = 'https://www.github.com/thejamesthomas/javabank'
                    }

                    issueManagement {
                        url = 'http://github.com/thejamesthomas/javabank/issues'
                        system = 'GitHub Issues'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html4', true)
        }
    }
}